{ SlashCommandBuilder, ChatInputCommandInteraction, CacheType } from discord.js
{ restart } from ../index.civet
{ exec } from node:child_process
fs from node:fs
path from node:path
config from ../config.civet

data := new SlashCommandBuilder()
		.setName "update"
		.setDescription "Updates the Civet package."
		.addBooleanOption(&.setName("detatched").setDescription("Whether or not to detatch the process.").setRequired(false))

async function execute(interaction: ChatInputCommandInteraction<CacheType>) {
	if config().admins.includes interaction.member?.user.id!
		exec "pnpm i @danielx/civet@latest", async =>
			newVersion := JSON.parse(fs.readFileSync(path.join("node_modules", "@danielx/civet", "package.json"), "utf8")).version

			try
				await interaction.reply { content: `Updated to \`@danielx/civet@${newVersion}\`.`, ephemeral: true }
			catch
				;

			detatched := (!!interaction.options.getBoolean "detatched") ?? true
			restart detatched
	else
		await interaction.reply { content: `You aren't an admin.`, ephemeral: true }
}

export {
	data,
	execute,
}